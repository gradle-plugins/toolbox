plugins {
    id 'java-library'
    id 'groovy-base'
    id 'java-test-fixtures'
    id 'dev.nokee.documentation.bintray-publish' version '0.1.13'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

repositories {
    jcenter()
    maven { url = 'https://dl.bintray.com/nokeedev/distributions-snapshots/' }
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    api project(':gradle-fixtures') // TODO: ONLY WHILE WE FINISH THE MIGRATION TO THE RUNNER API
    api project(':gradle-runner-kit-api')

    implementation(project(':gradle-runner-kit-impl')) {
        because 'we need the common executor classes'
    }

    implementation project(':gradle-fixtures-file-system') // TODO: shadow or rename project
    implementation 'org.apache.commons:commons-lang3:3.9' // TODO: shadow
    implementation 'dev.nokee:coreExec:0.5.0-dfbba94d' // TODO: shadow

    testImplementation platform('org.spockframework:spock-bom:1.2-groovy-2.5')
    testImplementation 'org.spockframework:spock-core'
//    testImplementation gradleTestKit()
    testImplementation testFixtures(project(':gradle-fixtures-file-system'))
//
//    testFixturesCompileOnly localGroovy() // to prevent build failures
    testFixturesImplementation project(':gradle-fixtures-file-system')
//    testFixturesImplementation testFixtures(project(':gradle-fixtures-file-system'))
//    testFixturesImplementation 'dev.nokee:coreExec:0.5.0-dfbba94d'
//    testFixturesImplementation 'commons-io:commons-io:2.6'
//    testFixturesImplementation 'org.apache.commons:commons-lang3:3.9'
}

def createWrapper = tasks.register('createWrapper', Wrapper) {
    it.jarFile = layout.buildDirectory.file('wrapper-support/gradle/wrapper/gradle-wrapper.jar')
    it.scriptFile = layout.buildDirectory.file('wrapper-support/gradlew')
}

tasks.processTestFixturesResources {
    from(createWrapper) {
        into('dev/gradleplugins/runnerkit/wrapper')
    }
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = "https://dl.bintray.com/gradle-plugins/distributions"
            ext.packageName = 'dev.gradleplugins:gradle-fixtures'
        }
    }
}

tasks.register('release') {
    dependsOn('publish')
}