plugins {
    id 'java-library'
    id 'groovy-base'
    id 'java-test-fixtures'
    id 'dev.nokee.documentation.bintray-publish' version '0.1.13'
}

import static dev.gradleplugins.GradleRuntimeCompatibility.*

java {
    sourceCompatibility = minimumJavaVersionFor(minimumGradleVersion)
    targetCompatibility = minimumJavaVersionFor(minimumGradleVersion)
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
//    maven { url = 'https://dl.bintray.com/nokeedev/distributions-snapshots/' }
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    api project(':gradle-fixtures') // ONLY WHILE WE FINISH THE MIGRATION TO THE RUNNER API

    implementation project(':gradle-fixtures-file-system')
    implementation 'com.google.guava:guava:28.2-jre'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'commons-io:commons-io:2.6'
    implementation 'dev.nokee:coreExec:latest.integration'
    compileOnly gradleTestKit()

    testImplementation platform('org.spockframework:spock-bom:1.2-groovy-2.5')
    testImplementation 'org.spockframework:spock-core'
    testImplementation gradleTestKit()
    testImplementation testFixtures(project(':gradle-fixtures-file-system'))

    testFixturesCompileOnly localGroovy() // to prevent build failures
    testFixturesImplementation project(':gradle-fixtures-file-system')
    testFixturesImplementation testFixtures(project(':gradle-fixtures-file-system'))
    testFixturesImplementation 'dev.nokee:coreExec:latest.integration'
    testFixturesImplementation 'commons-io:commons-io:2.6'
    testFixturesImplementation 'org.apache.commons:commons-lang3:3.9'
}

def createWrapper = tasks.register('createWrapper', Wrapper) {
    it.jarFile = layout.buildDirectory.file('wrapper-support/gradle/wrapper/gradle-wrapper.jar')
    it.scriptFile = layout.buildDirectory.file('wrapper-support/gradlew')
}

tasks.processTestFixturesResources {
    from(createWrapper) {
        into('dev/gradleplugins/fixtures/gradle/wrapper')
    }
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = "https://dl.bintray.com/gradle-plugins/distributions"
            ext.packageName = 'dev.gradleplugins:gradle-fixtures'
        }
    }
}

tasks.register('release') {
    dependsOn('publish')
}