plugins {
    id 'toolboxbuild.java-library'
    id 'groovy-base'
    id 'java-test-fixtures'
    id 'toolboxbuild.publish'
    id 'toolboxbuild.functional-test-repository'
}

description = 'Gradle runner kit implementation.'

repositories {
    gradlePluginDevelopment()
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    def commonsExecVersion = '1.3'
    def guavaVersion = '28.2-jre'
    def commonsLangVersion = '3.9'
    def commonsIoVersion = '2.6'
    implementation "org.apache.commons:commons-exec:${commonsExecVersion}"
    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "commons-io:commons-io:${commonsIoVersion}"
    implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"
    implementation 'net.rubygrapefruit:ansi-control-sequence-util:0.3'

    implementation project(':gradle-runner-kit-api')
    implementation project(':gradle-fixtures-file-system')
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'commons-io:commons-io:2.6'
    compileOnly gradleApi(minimumGradleVersion)

    testImplementation platform('org.spockframework:spock-bom:1.2-groovy-2.5')
    testImplementation 'org.spockframework:spock-core'
    testImplementation gradleTestKit(gradle.gradleVersion)
    testImplementation testFixtures(project(':gradle-fixtures-file-system'))
    testImplementation 'org.apache.commons:commons-lang3:3.9'
    testImplementation testFixtures(project(':gradle-runner-kit'))
}

def generatePluginUnderTestMetadataUnderTestTask = tasks.register('generatePluginUnderTestMetadataUnderTest') { task ->
    ext.outputFile = layout.buildDirectory.file('plugin-under-test-metadata.properties')
    task.outputs.file(ext.outputFile)
    task.doLast {
        ext.outputFile.get().asFile << "implementation-classpath = ${files('build/classes/java/main', 'foo.jar', 'bar.jar').asPath}\n"
    }
}

tasks.named('processTestResources', ProcessResources) { task ->
    task.from(generatePluginUnderTestMetadataUnderTestTask.map { it.outputFile })
}
