import static dev.gradleplugins.GradleRuntimeCompatibility.*

plugins {
    id 'org.jetbrains.kotlin.jvm' version "1.3.72"
    id 'toolboxbuild.publish'
    id 'com.gradle.plugin-publish' version '0.13.0'
    id 'toolboxbuild.jvm-base'
}
apply plugin: 'dev.gradleplugins.java-gradle-plugin'
apply plugin: 'dev.gradleplugins.gradle-plugin-unit-test'
apply plugin: 'dev.gradleplugins.gradle-plugin-functional-test'

description = 'Gradle plugin development plugins.'
version = '1.6.10'

gradlePlugin {
    plugins {
        gradlePluginBase {
            id = 'dev.gradleplugins.gradle-plugin-base'
            implementationClass = 'dev.gradleplugins.internal.plugins.GradlePluginDevelopmentBasePlugin'
        }
        javaGradlePlugin {
            id = 'dev.gradleplugins.java-gradle-plugin'
            implementationClass = 'dev.gradleplugins.internal.plugins.JavaGradlePluginDevelopmentPlugin'
        }
        groovyGradlePlugin {
            id = 'dev.gradleplugins.groovy-gradle-plugin'
            implementationClass = 'dev.gradleplugins.internal.plugins.GroovyGradlePluginDevelopmentPlugin'
        }
        gradlePluginDevelopment {
            id = 'dev.gradleplugins.gradle-plugin-development'
            implementationClass = 'dev.gradleplugins.internal.plugins.GradlePluginDevelopmentPlugin'
        }
        testingBase {
            id = 'dev.gradleplugins.gradle-plugin-testing-base'
            implementationClass = 'dev.gradleplugins.internal.plugins.GradlePluginDevelopmentTestingBasePlugin'
        }
        unitTest {
            id = 'dev.gradleplugins.gradle-plugin-unit-test'
            implementationClass = 'dev.gradleplugins.internal.plugins.GradlePluginDevelopmentUnitTestingPlugin'
        }
        functionalTest {
            id = 'dev.gradleplugins.gradle-plugin-functional-test'
            implementationClass = 'dev.gradleplugins.internal.plugins.GradlePluginDevelopmentFunctionalTestingPlugin'
        }
    }
    compatibility {
        minimumGradleVersion = project.minimumGradleVersion
    }
}

pluginBundle {
    website = 'https://gradleplugins.dev'
    vcsUrl = 'https://github.com/gradle-plugins/toolbox'
    description = 'Painless and fast Gradle plugin development ðŸš€'
    tags = ['gradle-plugin-development', 'gradle-plugin']

    plugins {
        gradlePluginBase {
            displayName = 'Gradle Plugin Development base plugin'
            description = 'Provides base support for developing Gradle plugin-related projects.'
            tags = ['gradle-plugin-development', 'gradle-plugin']
        }
        javaGradlePlugin {
            displayName = 'Java Gradle Plugin Development plugin'
            description = 'Provides support for developing Gradle plugins implemented in Java.'
            tags = ['gradle-plugin-development', 'gradle-plugin', 'java-gradle-plugin', 'java']
        }
        groovyGradlePlugin {
            displayName = 'Groovy Gradle Plugin Development plugin'
            description = 'Provides support for developing Gradle plugins implemented in Groovy.'
            tags = ['gradle-plugin-development', 'gradle-plugin', 'groovy-gradle-plugin', 'groovy']
        }
        gradlePluginDevelopment {
            displayName = 'Gradle Plugin Development plugin'
            description = 'Provides additional support for developing Gradle plugin-related projects.'
            tags = ['gradle-plugin-development', 'gradle-plugin', 'settings-plugin', 'project-plugin']
        }
        testingBase {
            displayName = 'Gradle Plugin Testing base plugin'
            description = 'Provides support for testing Gradle plugins.'
            tags = ['gradle-plugin-development', 'gradle-plugin', 'testing']
        }
        unitTest {
            displayName = 'Gradle Plugin Unit Test plugin'
            description = 'Provides support for unit testing Gradle plugins.'
            tags = ['gradle-plugin-development', 'gradle-plugin', 'unit-test']
        }
        functionalTest {
            displayName = 'Gradle Plugin Functional Test plugin'
            description = 'Provides support for functional testing Gradle plugins.'
            tags = ['gradle-plugin-development', 'gradle-plugin', 'functional-test']
        }
    }
}

test {
    dependencies {
        implementation spockFramework()
        implementation groovy()
        implementation "org.junit.jupiter:junit-jupiter:5.9.3"
        runtimeOnly "org.junit.vintage:junit-vintage-engine:5.9.3"
        implementation "org.hamcrest:hamcrest:2.2"
        implementation "org.mockito:mockito-core:4.2.0"
    }
    testTasks.configureEach { useJUnitPlatform() }
}

functionalTest {
    testTasks.configureEach {
        dependsOn({ allprojects
                .findAll { it.pluginManager.hasPlugin('toolboxbuild.functional-test-repository') }
                .collect { it.tasks.named('publishAllPublicationsToLocalRepositoryRepository') }
        })
        it.systemProperty('localRepository', layout.buildDirectory.dir('repository').get().asFile)
    }
    testingStrategies = [strategies.coverageForMinimumVersion, strategies.coverageForLatestGlobalAvailableVersion]
    dependencies {
        implementation spockFramework()
        implementation gradleTestKit()
        implementation groovy()
        implementation project(':gradle-plugin-development-test-fixtures')

        implementation "org.junit.jupiter:junit-jupiter:5.9.3"
        runtimeOnly "org.junit.vintage:junit-vintage-engine:5.9.3"
        implementation "org.hamcrest:hamcrest:2.2"
        implementation "dev.gradleplugins:gradle-build-script"
    }
    testTasks.configureEach { useJUnitPlatform() }
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'com.google.code.gson:gson:2.8.6'
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // Force local fixtures
    // TODO: Maybe look into dependency substitution
    functionalTestImplementation(project(':gradle-fixtures')) {
        capabilities {
            requireCapability("dev.gradleplugins:gradle-fixtures-spock-support:${project(':gradle-fixtures').version}")
        }
    }
}
