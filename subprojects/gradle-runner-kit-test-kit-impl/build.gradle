plugins {
    id 'toolboxbuild.java-library'
    id 'groovy-base'
    id 'java-test-fixtures'
    id 'toolboxbuild.publish'
    id 'dev.fukgradleplugins.functional-test-repository'
}

description = 'Gradle runner kit implementation for Gradle TestKit executor.'

repositories {
    gradlePluginDevelopment()
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    api project(':gradle-runner-kit-api')
    api(project(':gradle-runner-kit-impl')) { because 'we need the common executor classes' }
    implementation project(':gradle-fixtures-file-system')
    compileOnly gradleTestKit(minimumGradleVersion) // Good enough
}

def createWrapper = tasks.register('createWrapper', Wrapper) {
    it.jarFile = layout.buildDirectory.file('wrapper-support/gradle/wrapper/gradle-wrapper.jar')
    it.scriptFile = layout.buildDirectory.file('wrapper-support/gradlew')
}

tasks.processTestFixturesResources {
    from(createWrapper) {
        into('dev/gradleplugins/fixtures/gradle/wrapper')
    }
}

tasks.named('javadoc') { enabled = false }

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
}
