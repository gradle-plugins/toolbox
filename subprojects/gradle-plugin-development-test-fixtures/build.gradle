import static dev.gradleplugins.GradleRuntimeCompatibility.*

plugins {
    id 'toolboxbuild.java-library'
    id 'groovy'
    id 'toolboxbuild.publish'
}

description = 'Gradle plugin development plugins test fixtures.'
version = '1.6.11'

dependencies {
    compileOnly gradleApi(minimumGradleVersion)
    api 'dev.gradleplugins:gradle-fixtures-source-elements'
    implementation project(':gradle-fixtures')
    implementation(project(':gradle-fixtures')) {
        capabilities {
            requireCapability("dev.gradleplugins:gradle-fixtures-spock-support:${project(':gradle-fixtures').version}")
        }
    }
    implementation "org.codehaus.groovy:groovy-all:${groovyVersionOf(minimumGradleVersion)}"
}

sourceSets.main {
    dependencies.add(annotationProcessorConfigurationName, 'dev.gradleplugins:source-elements-annotation-processor')
    tasks.named(compileJavaTaskName, JavaCompile) {
        options.compilerArgs.add('-AbasePath=' + file('testProjects'))
        inputs.files(fileTree('testProjects').include('**/*.java', '**/*.groovy'))
    }
}